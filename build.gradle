// Build Script Classpath
buildscript {
    ext {
        springBootVersion = '3.4.1'
    }
    dependencies {
        // jib build requires this dependency;
        // see https://github.com/GoogleContainerTools/jib/issues/4235
        classpath group: 'org.apache.commons', name: 'commons-compress', version: '1.27.1'
        classpath group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.18.2'
    }
}

plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.google.cloud.tools.jib' version '3.4.4'
    id 'de.undercouch.download' version '5.6.0'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'org.sonarqube' version '6.0.1.5171'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'com.srcclr.gradle' version '3.1.12'
}

sourceCompatibility = JavaVersion.VERSION_17

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false

allprojects {
    group = gradle.projectGroup
    version = gradle.rbsVersion
    ext {
        artifactGroup = "${group}.buffer"
        swaggerOutputDir = "${buildDir}/generated"
        resourceDir = "${rootDir}/src/main/resources"
        isCiServer = System.getenv().containsKey("CI")
    }

    apply plugin: 'idea'
    idea {
        module {
            generatedSourceDirs = [file("${swaggerOutputDir}/src/main/java")]
            downloadJavadoc = true
        }
    }
}

repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Terra deps - we get Stairway via TCL
    implementation group: 'bio.terra', name: 'terra-common-lib', version: '1.1.27-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-cloud-resource-lib', version: '1.2.34-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: '0.113.38-SNAPSHOT'

    // Versioned direct deps
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '6.0.2'
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '6.0.2'
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.11.0'
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.135.0'
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.1.0'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '4.30.0'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.59'
    runtimeOnly group: 'org.postgresql', name: 'postgresql'

    // Google cloud open telemetry exporters
    var gcpOpenTelemetryExporterVersion = '0.33.0'
    implementation "com.google.cloud.opentelemetry:exporter-trace:${gcpOpenTelemetryExporterVersion}"
    implementation "com.google.cloud.opentelemetry:exporter-metrics:${gcpOpenTelemetryExporterVersion}"

    // Deps whose versions are controlled by Spring
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.13.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.12.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jdbc', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.11'

    // Swagger deps
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.18.2'
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.14'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.27'
    runtimeOnly group: 'org.webjars.npm', name: 'swagger-ui-dist', version: '5.18.2'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli', version: '3.0.61'

    // Test deps
    testImplementation 'org.awaitility:awaitility:4.2.2'
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.11.0'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: "${springBootVersion}"

    // Transitive dependency constraints due to security vulnurabilities in prior versions.
    // These are not directly included, they are just constrained if they are pulled in as
    // transitive dependencies.
    constraints {
        implementation('org.yaml:snakeyaml:2.3')
        implementation('com.nimbusds:nimbus-jose-jwt:9.48')
        implementation('io.projectreactor.netty:reactor-netty-http:1.2.1')
        implementation('com.fasterxml.jackson:jackson-bom:2.18.2')
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def gradleIncDir= "$rootDir/gradle"
apply from: "$gradleIncDir/jacoco.gradle"
apply from: "$gradleIncDir/javadoc.gradle"
apply from: "$gradleIncDir/jib.gradle"
apply from: "$gradleIncDir/sonarqube.gradle"
apply from: "$gradleIncDir/spotless.gradle"
apply from: "$gradleIncDir/swagger-server.gradle"
apply from: "$gradleIncDir/testing.gradle"
apply from: "$gradleIncDir/srcclr.gradle"

sourceSets.main.java.srcDir "${swaggerOutputDir}/src/main/java"
sourceSets.test.resources.srcDir 'config/' // Allow unit tests to directly load config files.
