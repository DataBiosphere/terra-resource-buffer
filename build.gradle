// Build Script Classpath
buildscript {
    ext {
        springBootVersion = '2.7.10'
    }
}

plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless' version '6.16.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.github.spotbugs' version '5.0.13'
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id 'de.undercouch.download' version '5.1.0'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'org.springframework.boot' version "${springBootVersion}"
}

sourceCompatibility = JavaVersion.VERSION_17

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false

allprojects {
    group = gradle.projectGroup
    version = gradle.rbsVersion
    ext {
        artifactGroup = "${group}.buffer"
        swaggerOutputDir = "${buildDir}/generated"
        resourceDir = "${rootDir}/src/main/resources"
        isCiServer = System.getenv().containsKey("CI")
    }

    apply plugin: 'idea'
    idea {
        module {
            generatedSourceDirs = [file("${swaggerOutputDir}/src/main/java")]
            downloadJavadoc = true
        }
    }
}

repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Terra deps - we get Stairway via TCL
    implementation group: 'bio.terra', name: 'terra-common-lib', version: '0.0.92-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-cloud-resource-lib', version: '1.2.13-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: '0.113.25-SNAPSHOT'

    // Versioned direct deps
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.9'
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.120.11'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'io.opencensus', name: 'opencensus-exporter-stats-stackdriver', version: '0.31.1'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '4.15.0'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.52'
    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.4.1'
    // CRL uses version 2.0.3 which is not compatible with other RBS dependencies,
    // force the lower version for now.
    implementation('org.slf4j:slf4j-api') {
        version {
            strictly '1.7.36'
        }
    }

    // Deps whose versions are controlled by Spring
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.11.1'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jdbc', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"


    // Swagger deps
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.13.3'
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.6'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'
    runtimeOnly group: 'org.webjars.npm', name: 'swagger-ui-dist', version: '3.36.2'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli', version: '3.0.35'

    // Test deps
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.9'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: "${springBootVersion}"

    // Transitive dependency constraints due to security vulnurabilities in prior versions.
    // These are not directly included, they are just constrained if they are pulled in as
    // transitive dependencies.
    constraints {
        implementation('com.google.protobuf:protobuf-java:3.21.10')
        implementation('org.yaml:snakeyaml:1.33')
        implementation('com.nimbusds:nimbus-jose-jwt:9.25.6')
        implementation('io.projectreactor.netty:reactor-netty-http:1.0.24')
        implementation('com.fasterxml.jackson:jackson-bom:2.14.2')
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def gradleIncDir= "$rootDir/gradle"
apply from: "$gradleIncDir/dependency-locking.gradle"
apply from: "$gradleIncDir/jacoco.gradle"
apply from: "$gradleIncDir/javadoc.gradle"
apply from: "$gradleIncDir/jib.gradle"
apply from: "$gradleIncDir/sonarqube.gradle"
apply from: "$gradleIncDir/spotbugs.gradle"
apply from: "$gradleIncDir/spotless.gradle"
apply from: "$gradleIncDir/swagger-server.gradle"
apply from: "$gradleIncDir/testing.gradle"

sourceSets.main.java.srcDir "${swaggerOutputDir}/src/main/java"
sourceSets.test.resources.srcDir 'config/' // Allow unit tests to directly load config files.
