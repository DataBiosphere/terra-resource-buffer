plugins {
    id 'idea'
    id 'java'

    id 'com.diffplug.spotless' version '5.8.2'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'com.github.spotbugs' version '4.5.1'
    id 'com.google.cloud.tools.jib' version '2.6.0'
    id 'de.undercouch.download' version '4.0.0'
    id "jacoco"
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
}

sourceCompatibility = JavaVersion.VERSION_11

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false

allprojects {
    group = 'bio.terra'
    version = System.getenv('SERVICE_VERSION') != null ? System.getenv('SERVICE_VERSION') : '0.0.1-SNAPSHOT'
    ext['log4j2.version'] = '2.16.0' // until Spring Boot 2.6.2
    ext {
        artifactGroup = "${group}.buffer"
        swaggerOutputDir = "${buildDir}/generated"
        resourceDir = "${rootDir}/src/main/resources"
    }

    // specifying versions for dependencies, not adding the dependencies themselves (yet)
    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency group: "io.swagger.core.v3", name: "swagger-annotations", version: "2.1.5"
            dependency group: "io.swagger.codegen.v3", name: "swagger-codegen-cli", version: "3.0.22"

            // PF-866: This will need to manually be kept in sync with the version of cloud-resource-schema
            // specified by bio.terra.cloud-resource-lib:platform, as the dependency-management plugin does
            // not support gradle platform plugin:
            //
            // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/211

            dependency group: 'bio.terra.cloud-resource-lib', name: 'cloud-resource-schema', version: "0.9.0"
        }
    }

    apply plugin: "idea"
    idea {
        module {
            generatedSourceDirs = [file("${swaggerOutputDir}/src/main/java")]
            downloadJavadoc = true
        }
    }
}

repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Terra deps - we get Stairway via TCL
    implementation group: 'bio.terra', name: 'terra-common-lib', version: '0.0.52-SNAPSHOT'

    // When updating platform version, be sure to keep Spring dependency-management plugin stanza's
    // version of cloud-resource-schema in sync with the platform (PF-866).
    implementation platform('bio.terra.cloud-resource-lib:platform:0.2.0')
    implementation group: 'bio.terra.cloud-resource-lib', name: 'cloud-resource-schema'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'common'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-billing'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-cloudresourcemanager'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-compute'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-dns'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-iam'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-serviceusage'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-storage'

    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: '0.4.0-SNAPSHOT'

    // Versioned direct deps
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.7.3'
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.104.1'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'io.opencensus', name: 'opencensus-exporter-stats-stackdriver', version: '0.28.+'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-gcp-starter-logging', version: '1.2.6.RELEASE'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '3.10.0'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.46'
    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.2.12'

    // Deps whose versions are controlled by Spring
    implementation group: 'org.apache.commons', name: 'commons-dbcp2'
    implementation group: 'org.apache.commons', name: 'commons-lang3'
    implementation group: 'org.apache.commons', name: 'commons-pool2'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    // Swagger deps
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.11.2'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: '2.11.2'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'
    runtimeOnly group: 'org.webjars.npm', name: 'swagger-ui-dist', version: '3.36.2'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli'

    // Test deps
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.7.3'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
}

dependencyLocking {
    lockAllConfigurations() // see https://docs.gradle.org/current/userguide/dependency_locking.html
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

java {
    withJavadocJar()
}

// Download and extract the Cloud Profiler Java Agent
ext {
    // where to place the Cloud Profiler agent in the container
    cloudProfilerLocation = '/opt/cprof'

    // location for jib extras, including the Java agent
    jibExtraDirectory = "${buildDir}/jib-agents"
}
task downloadProfilerAgent(type: Download) {
    // where to download the Cloud Profiler agent https://cloud.google.com/profiler/docs/profiling-java
    src 'https://storage.googleapis.com/cloud-profiler/java/latest/profiler_java_agent.tar.gz'
    dest "${buildDir}/cprof_java_agent_gce.tar.gz"
}
task extractProfilerAgent(dependsOn: downloadProfilerAgent, type: Copy) {
    from tarTree(downloadProfilerAgent.dest)
    into "${jibExtraDirectory}/${cloudProfilerLocation}"
}

jib {
    from {
        image = "adoptopenjdk:11-jre-hotspot"
    }
    extraDirectories {
        paths = [file(jibExtraDirectory)]
    }
    container {
        filesModificationTime = java.time.ZonedDateTime.now().toString() // to prevent ui caching
        jvmFlags = [
                '-agentpath:' + cloudProfilerLocation + '/profiler_java_agent.so=' +
                        '-cprof_service=bio.terra.buffer' +
                        ',-cprof_service_version=' + version +
                        ',-cprof_enable_heap_sampling=true' +
                        ',-logtostderr' +
                        ',-minloglevel=2'
        ]
    }
}

tasks.jib.dependsOn extractProfilerAgent
tasks.jibDockerBuild.dependsOn extractProfilerAgent
tasks.jibBuildTar.dependsOn extractProfilerAgent

// Linter
spotless {
    java {
        googleJavaFormat()
        targetExclude "${buildDir}/**"
    }
}

// CRL schema extraction
configurations {
    cloudResourceSchema { transitive = false }
}
dependencies {
    cloudResourceSchema group: 'bio.terra.cloud-resource-lib', name: 'cloud-resource-schema'
}
task unzipCloudResourceSchema(type: Copy) {
    from zipTree(configurations.cloudResourceSchema.singleFile).matching {
        include 'cloud_resources_uid.yaml'
    }
    into "${buildDir}/crlSchema"
}

// OpenAPI/Swagger Server Generation
swaggerSources {
    server {
        inputFile = file("${resourceDir}/api/service_openapi.yaml")
        code {
            language = "spring"
            library = "spring-boot"
            outputDir = file("${swaggerOutputDir}")
            components = ["models", "apis"]
            wipeOutputDir = false
            rawOptions = [
                    "--api-package", "${artifactGroup}.generated.controller",
                    "--model-package", "${artifactGroup}.generated.model",
                    "-D", "interfaceOnly=true," +
                            "useTags=true," +
                            "dateLibrary=java8"
            ]
        }
    }

    resource {
        inputFile = file("${resourceDir}/config/resource_schema.yaml")
        code {
            language = "spring"
            library = "spring-boot"
            outputDir = file("${swaggerOutputDir}")
            components = ["models"]
            wipeOutputDir = false
            rawOptions = [
                    "--model-package", "${artifactGroup}.generated.model",
                    "-D", "interfaceOnly=true," +
                            "useTags=true," +
                            "dateLibrary=java8"
            ]
        }
    }
}

// Static analysis
spotbugs {
    effort = 'max'
    // This makes the "html" reports come out in plain text so you can just open the file in IntelliJ
    // and look at your bugs instead of having to switch to a browser.
    extraArgs = ['-emacs']
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.html")
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.html")
        }
    }
}

tasks.withType(JacocoReport) {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.1
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

// Testing config

// This is the path to the default Google service account for the buffer service to run as.
def googleCredentialsFile = "${projectDir}/src/test/resources/rendered/sa-account.json"

tasks.withType(Test) {
    environment = [
            'GOOGLE_APPLICATION_CREDENTIALS': "${googleCredentialsFile}"
    ]
    testLogging {
        events = ["passed", "failed", "skipped"]
        // Causes the correct line to be reported on an exception.
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        reports {
            // Write XML file (used by CircleCI, Jenkins, etc) to build/test-results/*
            junitXml.enabled = true
            // Write human-readable test report to build/reports/tests/*
            html.enabled = true
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags 'unit'
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    // Force tests to always be re-run, since integration tests involve communicating with external
    // resources.
    outputs.upToDateWhen { false }
}

// Note The Open API schema depends on an external library - cloud-resource-schema, so need to unzip it first.
swaggerSources.server.code.dependsOn tasks.unzipCloudResourceSchema
compileJava.dependsOn swaggerSources.server.code, swaggerSources.resource.code, spotlessApply
sourceSets.main.java.srcDir "${swaggerOutputDir}/src/main/java"
sourceSets.test.resources.srcDir "config/" // Allow unit tests to directly load config files.
sourceSets {
    jacoco {
        toolVersion '0.8.6'
    }
}
